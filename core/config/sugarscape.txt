# -----------------------------------------------------------------------------
# Sugarscape : Simplified implementation of the Sugarscape system
#              https://en.wikipedia.org/wiki/Sugarscape
# Description :
# - This version of Sugarscape is based on 1 vegetal and 2 animal states:
# - 'grass' agents grow by germination every 3 time steps
# - 'sheep' agents start with 40 'grass' points and loose 1 point per time step
# - 'sheep' agents have random movements as long as they are not 'hungry'
# - 'hungry' agents move on neighbouring cell with maximal 'grass' field
# - 'hungry' agents eat all 'grass' agents they met
# - 'hungry' agents go back to 'sheep' state when they eat enough 'grass'
# -----------------------------------------------------------------------------

world 32 32 #FFF

vegetal grass #0C0
var amount 5 1
var grass 5 0               # 'grass' field broadcasts up to distance 5
field grass -1
status amount < 1 death      # 'grass' agent disapears when eated by a 'sheep'
var seed 3 -1               # 'grass' germination requires 3 time steps
birth seed = 0 grass

animal sheep #FF5
var fed 40 -1             # sheep starts with 40 initial 'grass' points
status fed < 30 hungry    # sheep becomes hungry when 'grass' < 30

animal hungry #F50
var fed 30 -1
var smell 0 0
sensor smell grass 1        # hungry sheep tries to find grass
sensor fed amount 1
status fed < 1 death      # sheep dies when 'fed' drops to 0
status fed > 40 sheep     # back to 'sheep' state when 'fed' > 40

# initial configuration (4 blocks of grass, and 2 lines of sheep)
agent grass (0:4,0:4) (0:4,28:32) (27:31,0:4) (27:31,27:31)
agent sheep (8,14:18) (24,14:18)
# -----------------------------------------------------------------------------